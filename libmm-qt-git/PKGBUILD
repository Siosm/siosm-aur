# Maintainer: Timoth√©e Ravier <tim@siosm.fr>
# Previous maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmm-qt-git
pkgver=137.a7a87cd
pkgrel=1
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('qt4' 'modemmanager')
makedepends=('cmake' 'git' 'doxygen')
conflicts=('libmm-qt')
provides=('libmm-qt')
source=('git://anongit.kde.org/libmm-qt')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname/-git}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname/-git} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
