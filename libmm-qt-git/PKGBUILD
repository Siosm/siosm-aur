# Maintainer: Timoth√©e Ravier <tim@siosm.fr>
# Previous maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmm-qt-git
_gitname=libmm-qt
pkgver=70.c2e805c
pkgrel=1
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('qt4')
makedepends=('cmake' 'git')
conflicts=('libmm-qt')
provides=('libmm-qt')
source=('git://anongit.kde.org/libmm-qt')
sha256sums=('SKIP')

pkgver() {
  cd ${_gitname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  rm -rf build
  mkdir build
}

build() {
  cd build
  cmake ../${_gitname} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
