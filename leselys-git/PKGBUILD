# Maintainer: Timoth√©e Ravier <tim@siosm.fr>

pkgname=leselys-git
_gitname=leselys
pkgver=0.2.5.388.a887384
pkgrel=1
pkgdesc='Web based elegant RSS reader'
arch=('any')
url='https://github.com/socketubs/leselys'
license=('AGPL3')
depends=('mongodb' 'python2-lxml' 'python2-itsdangerous' 'python2-flask'
         'python2-docopt' 'python2-celery' 'python2-pymongo' 'python-bcrypt'
         'python2-feedparser' 'python2-requests')
source=("git+https://github.com/socketubs/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
  cd ${_gitname}
  echo -n $(grep version leselys/__init__.py | awk '{ print $3 }' | sed "s|'||g")
  echo .$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd ${_gitname}
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:
