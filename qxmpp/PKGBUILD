# Maintainer: Spike29 <leguen.yannick@gmail.com>
# Contributor: Samir Faci <csgeek@archlinux.us>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname='qxmpp'
pkgver='0.7.6'
pkgrel='1'
pkgdesc='An XMPP client library based on Qt & C++'
arch=('i686' 'x86_64')
url='http://code.google.com/p/qxmpp/'
license=('LGPL 2.1')
depends=('qt4>=4.5')
optdepends=('speex: required to enable speex audio codec'
            'libvpx: required to enable vpx video codec'
            'libtheora: required to enable theora video codec')
conflicts=('qxmpp-git' 'qxmpp-leechcraft-git')
source=("http://qxmpp.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
sha1sums=('a87b4b5c94d1f4dc723cbbb7799cf4067c7e5ea2')

build() {
	cd ${pkgname}-${pkgver}
	mkdir -p build
	cd build
	qmake-qt4 PREFIX=/usr ..

	# In order to enable speex audio codec, vpx video codec or theora video codec,
	# add QXMPP_USE_SPEEX=1, QXMPP_USE_VPX=1 and QXMPP_USE_THEORA=1 to qmake arguments.

	make
}

package() {
	cd ${pkgname}-${pkgver}/build

	make INSTALL_ROOT="${pkgdir}" install
}
