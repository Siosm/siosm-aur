pkgname=k9copy
pkgver=2.3.8
pkgrel=4
pkgdesc="A small utility which allows the copy of DVD under Linux"
arch=('i686' 'x86_64')
url="http://k9copy.sourceforge.net"
license=('GPL')
depends=('kdebase-runtime' 'libmpeg2' 'xine-lib' 'dvd+rw-tools' 'dvdauthor' 'libdvdnav')
makedepends=('automoc4' 'cmake')
install=k9copy.install
source=("http://downloads.sourceforge.net/project/$pkgname/k9copy-kde4/$pkgver/$pkgname-$pkgver-Source.tar.gz"
	'ffmpeg.patch')
md5sums=('584f6cc50f28befbeb0d5c1ec9476456'
	 '7b2ad54fb4aaa7c714cfaff406569146')

build() {
  cd "$srcdir/$pkgname-$pkgver-Source"

  # fix build for recent versions of ffmpeg and link directly to libav and libdvd
  patch -p1 -i ../ffmpeg.patch

  # fix desktop files
  sed -e 's|Name=k9copy|Name=K9copy|g' \
	-e '/xdg-open/d' -e '/Encoding/d' -e '/MimeTypes/d' \
	-i k9copy{,_assistant}.desktop

  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-Source"

  make DESTDIR="$pkgdir" install
}

