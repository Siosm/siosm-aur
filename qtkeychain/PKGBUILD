# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# Contributor: Teo Mrnjavac <teo@kde.org>

pkgname=qtkeychain
pkgver=0.1
pkgrel=3
pkgdesc="Provides support for secure credentials storage."
arch=('i686' 'x86_64')
url='https://github.com/frankosterfeld/qtkeychain'
license=('BSD')
depends=('qt4')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.zip::https://github.com/frankosterfeld/qtkeychain/archive/${pkgver}.zip"
        'prefer-qt4.patch')
md5sums=('a1d11a611b27759a531896b57be15947'
         'a2c46e61af02100a6f67487ffeb8a3d0')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -Np1 -i ../prefer-qt4.patch
}

build() {
    rm -rf ${pkgname}-${pkgver}-build
    mkdir ${pkgname}-${pkgver}-build

    cd ${pkgname}-${pkgver}-build
    cmake -DBUILD_WITH_QT4=on \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=RelWithDebInfo \
          ../${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}-build

    make DESTDIR="${pkgdir}" install

    install -Dm 0644 "${srcdir}"/${pkgname}-${pkgver}/COPYING \
      "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
