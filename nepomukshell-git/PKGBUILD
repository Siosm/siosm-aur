# Contributor: Michal Malek <michalm@jabster.pl>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=nepomukshell-git
_gitname=nepomukshell
pkgver=0.8.0.193.a3005ea
pkgrel=1
pkgdesc="Allows to browse, query, and edit Nepomuk resources"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/sdk/nepomukshell"
license=('GPL3')
depends=('kdelibs')
makedepends=('git' 'cmake' 'automoc4')
provides=('nepomukshell')
conflicts=('nepomukshell')
source=('git://anongit.kde.org/nepomukshell')
sha256sums=('SKIP')

pkgver() {
	cd ${_gitname}
	echo '0.8.0'.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd ${_gitname}
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
	make
}

package() {
	cd ${_gitname}
	make DESTDIR="${pkgdir}" install
}
