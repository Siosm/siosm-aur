# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=etherpad-lite
pkgver=1.3.0
pkgrel=3
pkgdesc="Lightweight fork of etherpad based on javascript"
arch=(any)
url='http://etherpad.org'
_watch=('http://etherpad.org','Documentation <small>v([\d.]*)</small>')
license=('GPL2')
depends=('curl' 'python2' 'openssl' 'nodejs')
optdepends=('sqlite: to use sqlite as databse'
            'mariadb: to use mysql/mariadb as database'
            'postgresql: to use postgresql as database')
conflicts=('etherpad-lite-git')
backup=(usr/share/webapps/etherpad-lite/settings.json)
install='etherpad-lite.install'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ether/${pkgname}/archive/${pkgver}.tar.gz"
	"etherpad-lite.service"
	"etherpad-lite.tmpfile.d")
sha512sums=('033d54c5ec9daa23d8f57b0543b8d87cb76e3568f888f5f380454821ad2c6171e1c6b64e56a4e9476c2f757da698fb75a170693c963334bcc42dfd4145fb181d'
	    '5f1273a9effcc17c81cdc275a97c4f29ad6145f42969fb1c06d5e963300908717d018cc2c0ce26a5b1ad8b86dfea6dd9159dd528a6585307c4079b5b4417b0d6'
	    '8172f4fc2d1f394046cb9215b50917cc5f42559d030b408c5c1919e3b62e53776d56ec00674260a332533545aa08160993e8c61663ce2786c9d97a2dafd560e4')

build() {
  cd ${pkgname}-${pkgver}
  ./bin/installDeps.sh
  npm install sqlite3 --loglevel warn
}

package() {
  cd ${pkgname}-${pkgver}
  rm -r start.bat var
  install -dm 755 "${pkgdir}"/usr/share/webapps/${pkgname}
  cp -a . "${pkgdir}"/usr/share/webapps/${pkgname}
  install -Dm644 "${srcdir}"/etherpad-lite.service "${pkgdir}"/usr/lib/systemd/system/etherpad-lite.service
  install -Dm644 "${srcdir}"/etherpad-lite.tmpfile.d "${pkgdir}"/usr/lib/tmpfiles.d/etherpad-lite.conf
  install -D LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
