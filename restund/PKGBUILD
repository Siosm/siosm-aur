# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=restund
pkgver=0.4.1
pkgrel=1
pkgdesc="A modular and flexible STUN and TURN Server, with IPv4 and IPv6 support"
arch=('i686' 'x86_64')
url="http://www.creytiv.com/"
license=('custom:BSD')
depends=("libre>=$pkgver" 'libmysqlclient')
optdepends=('munin: for monitoring')
backup=("etc/$pkgname.conf")
source=("http://www.creytiv.com/pub/$pkgname-$pkgver.tar.gz" $pkgname{,-args}.sh)
md5sums=('e11da0acd10af42ce6101f3106cd34dc'
         '40ac82d7b1a888b74006a088d48899bc'
         '18354f901fb74cb620a8753a0b4d6fd4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # restund.conf module path fix
  sed -i 's/\/local//' etc/$pkgname.conf

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DATADIR=/usr/lib \
       DESTDIR="$pkgdir" install

  # license
  install -Dm644 docs/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # restund.conf
  install -Dm644 etc/$pkgname.conf \
    "$pkgdir/etc/$pkgname.conf"

  # daemon
  install -Dm755 ../$pkgname.sh \
    "$pkgdir/etc/rc.d/$pkgname"

  # daemon conf
  install -Dm644 ../$pkgname-args.sh \
    "$pkgdir/etc/conf.d/$pkgname"
}
