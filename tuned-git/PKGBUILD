# Maintainer: Timoth√©e Ravier < tim at siosm dot fr >
# Contributor: Tom < reztho at archlinux dot org >

pkgname=tuned-git
pkgver=2.2.0.44.g15ec06a
pkgrel=1
pkgdesc='Daemon that performs monitoring and adaptive configuration of devices in the system'
arch=('any')
url='https://fedorahosted.org/tuned/'
license=('GPL')
depends=('ethtool' 'python2-configobj' 'python2-pyudev' 'python2-gobject2'
         'python2-decorator' 'python2-dbus')
optdepends=('virt-what' 'systemtap')
makedepends=('git')
provides=('tuned')
backup=('etc/tuned/active_profile')
install="${pkgname/-git}.install"
source=("${pkgname}::git://git.fedorahosted.org/git/${pkgname/-/.}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  local ver=$(git describe --tags | sed 's|-|.|g')
  echo ${ver:1}
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install

  # Move binaries from /usr/sbin to /usr/bin
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin/

  # Patching for using python2
  find "${pkgdir}"/usr/bin/ -type f -exec sed -i 's@#!/usr/bin/python@#!/usr/bin/python2@' {} \;

  # Move systemd service unit
  install -Dm 0644 "${pkgdir}"/tuned.service "${pkgdir}"/usr/lib/systemd/system/tuned.service
  rm "${pkgdir}"/tuned.service

  # Remove /run and /usr/sbin directories
  rm -r "${pkgdir}"/run "${pkgdir}"/usr/sbin
}

# vim:set ts=2 sw=2 et:
