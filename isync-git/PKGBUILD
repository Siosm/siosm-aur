# Contributor: Mark Pustjens <pustjens@dds.nl>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>
# Previous Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Previous Contributor: Giorgio Lando <patroclo7@gmail.com>
# Previous Contributor: Leslie P. Polzer <polzer@gnu.org>

pkgname=isync-git
pkgver=1.1.0.507.b1ff34d
pkgrel=1
pkgdesc="IMAP mail synchronizer for offline/batch mail editing"
url='http://isync.sourceforge.net'
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('perl-timedate')
depends=('openssl')
conflicts=('isync')
replaces=('isync')
source=("${pkgname}::git://git.code.sf.net/p/isync/isync")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  echo -n $(grep -m 1 -o -E "[0-9.]*" configure.ac)
  echo '.'$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd ${pkgname}
  ./autogen.sh
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
