# Contributor: Matthew Tompsett <matthewbpt@gmail.com>
# Contributor: William McInnis <wmcinnis1983@gmail.com>
# Maintainer: max.bra <max.bra at alice dot it>

pkgname=jupiter
pkgver=0.1.4
pkgrel=1
pkgdesc="Simple, easy to use hardware and power management applet for all i386 and x86_64 Linux laptops, and netbooks"
url="http://sourceforge.net/projects/jupiter"
license=('GPL')
makedepends=('rpmextract')
depends=('acpid' 'xorg-xinput' 'pygtk' 'xorg-xrandr' 'sudo')
optdepends=('rfkill: Tool to query and set the rfkill state of wireless devices' 'libnotify: Desktop notification library')
arch=('any')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}-1.noarch.rpm sudo_jupiter)
md5sums=('22682a2ab7c68c791d62ebf5eab7c49c' '4612aafbc6f808cdeb0d97a58b030dca')

build() {
  rpmextract.sh *.rpm

  sed -e 's|!/usr/bin/env python|!/usr/bin/env python2|' -i "$srcdir"/usr/bin/$pkgname
}

package() {
  cp -R "$srcdir"/{etc,usr} "$pkgdir"/

  install -d -m775 "$pkgdir"/var/jupiter

  install -d -m750 "$pkgdir"/etc/sudoers.d
  install -m440 "$srcdir"/sudo_jupiter "$pkgdir"/etc/sudoers.d/$pkgname
}

