# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is RelWithDebInfo to help with debugging.

_buildtype="Release"

##############################################################

pkgname=ocsync
pkgver=0.70.7
pkgrel=1
pkgdesc="A file synchronizer especially designed for you, the normal user."
arch=("i686" "x86_64")
url="http://www.csync.org"
license=('GPL2')
depends=('sqlite3' 'iniparser' 'neon' 'smbclient' 'libssh')
makedepends=('cmake')
#optdepends=('samba: smb support' 'libssh: sftp support')
provides=('csync' 'csync-owncloud')
conflicts=('csync' 'csync-owncloud')
source=("http://download.owncloud.com/download/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('571c7a822a8e30966c3a7e61a5218b49e8d52e70e8a8ec80d16db786d60bb324')

# Clean options array to strip pkg if release buildtype is chosen
if [[ ${_buildtype} == "Release" ]] || [[ ${_buildtype} == "release" ]]; then
  options=()
fi

build() {
  if [[ -e ${pkgname}-${pkgver}-build ]]; then
    rm -rf ${pkgname}-${pkgver}-build
  fi
  mkdir ${pkgname}-${pkgver}-build
  cd ${pkgname}-${pkgver}-build

  cmake -DCMAKE_BUILD_TYPE=${_buildtype} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONF_INSTALL_DIR=/etc \
        ../${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}-build
  make DESTDIR="${pkgdir}" install
}
