# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=ocsync
pkgver=0.90.0
pkgrel=1
pkgdesc="File synchronizer especially designed for you, the normal user"
arch=('i686' 'x86_64')
url='http://www.csync.org'
license=('GPL2')
depends=('sqlite3' 'iniparser' 'neon' 'libssh')
makedepends=('cmake')
provides=('csync' 'csync-owncloud')
conflicts=('csync' 'csync-owncloud')
backup=('etc/ocsync/ocsync.conf' 'etc/ocsync/ocsync_exclude.conf')
source=("http://download.owncloud.com/download/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('4531d196637829284704ce29816b2f6f9f8e61ca8dd61e6743774d085392301c')

build() {
  rm -rf ${pkgname}-${pkgver}-build
  mkdir ${pkgname}-${pkgver}-build

  cd ${pkgname}-${pkgver}-build

  cmake -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONF_INSTALL_DIR=/etc \
        ../${pkgname}-${pkgver}

  make
}

package() {
  cd ${pkgname}-${pkgver}-build
  make DESTDIR="${pkgdir}" install
}
