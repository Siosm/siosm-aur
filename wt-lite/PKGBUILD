# $Id: PKGBUILD 79020 2012-10-29 05:20:29Z svenstaro $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt-lite
_pkgname=wt
pkgver=3.2.3rc2
_pkgver=3.2.3-rc2
pkgrel=1
pkgdesc="a C++ library and application server for developing and deploying web applications - Lite version with less dependencies"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
replaces=('wt=3.2.3rc2')
provides=('wt=3.2.3rc2')
conflicts=('wt')
depends=('boost-libs')
makedepends=('boost' 'cmake>=2.8' 'zlib' 'fcgi')
optdepends=('openssl: for SSL support in built-in webserver'
	    'zlib: for compression in HTTP protocol'
	    'fcgi: for FastCGI support')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/${_pkgname}-${_pkgver}.tar.gz)
md5sums=('64097d4d5fe346fe9a09f5789ec54598')

build() {
  cd ${srcdir}/${_pkgname}-${_pkgver}

  mkdir -p build
  cd build
      #-DCMAKE_EXE_LINKER_FLAGS="-lboost_random" \
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DCONNECTOR_FCGI=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DBUILD_EXAMPLES=OFF \
      -BUILD_TESTS=OFF \
      -DENABLE_GM=OFF \
      -DENABLE_HARU=OFF \
      -DENABLE_PANGO=OFF \
      -DENABLE_SQLITE=OFF \
      -DENABLE_POSTGRES=OFF \
      -DENABLE_FIREBIRD=OFF \
      -DENABLE_QT4=OFF \
      ..
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${_pkgver}/build
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/share/cmake-2.8/Modules
  mv ${pkgdir}/usr/cmake/FindWt.cmake ${pkgdir}/usr/share/cmake-2.8/Modules/
  rm -r ${pkgdir}/usr/cmake
  rm -rf $pkgdir/var/run
}
