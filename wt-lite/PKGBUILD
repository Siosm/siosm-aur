# $Id: PKGBUILD 93696 2013-07-08 13:32:56Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt-lite
_pkgname=wt
pkgver=3.3.0
pkgrel=1
pkgdesc="C++ library and application server for developing and deploying web applications - Lite version with less dependencies"
arch=('i686' 'x86_64')
url='http://www.webtoolkit.eu/'
license=('GPL')
replaces=('wt=3.3.0')
provides=('wt=3.3.0')
conflicts=('wt')
depends=('boost-libs')
makedepends=('boost' 'cmake>=2.8' 'zlib' 'fcgi')
optdepends=('openssl: for SSL support in built-in webserver'
	    'zlib: for compression in HTTP protocol'
	    'fcgi: for FastCGI support')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('b7cefccd765ec52f76625eabc1905a9fe22e953cc29a49d51f0ec315a76ab8e2')

build() {
  cd ${_pkgname}-${pkgver}

  [[ -d build ]] && rm -r build
  mkdir -p build
  cd build
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DCONNECTOR_FCGI=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DBUILD_EXAMPLES=OFF \
      -BUILD_TESTS=OFF \
      -DENABLE_GM=OFF \
      -DENABLE_HARU=OFF \
      -DENABLE_PANGO=OFF \
      -DENABLE_SQLITE=OFF \
      -DENABLE_POSTGRES=OFF \
      -DENABLE_FIREBIRD=OFF \
      -DENABLE_QT4=OFF \
      ..
  make
}

package() {
  cd ${_pkgname}-${pkgver}/build
  make DESTDIR="${pkgdir}" install

  install -dm 644 "${pkgdir}"/usr/share/cmake-2.8/Modules
  mv "${pkgdir}"/usr/cmake/FindWt.cmake "${pkgdir}"/usr/share/cmake-2.8/Modules/
  rm -r "${pkgdir}"/usr/cmake
  rm -rf "${pkgdir}"/var/run
}
