# Maintainer: Michal Zalewski <lcamtuf[at]coredump[dot]cx>
# Contributor: cbreaker <cbreaker[at]tlen[dot]pl>]
# Contributor: Tino Reichardt
# Contributor: Timoth√©e Ravier <tim[at]siosm[dot]fr>

# additional tools: p0f-client & p0f-sendsyn, n to remove them from package
_p0ftools=y
# additional tool: p0f-sendsyn6 (ipv6 p0f-sendsyn), n to remove it from package
_p0ftools6=y

pkgname=p0f
pkgver=3.05b
pkgrel=1
pkgdesc="Purely passive TCP/IP traffic fingerprinting tool "
url="http://lcamtuf.coredump.cx/p0f3/"
arch=('x86_64' 'i686')
license=('GPLv3')
depends=('libpcap')
install=p0f.install
source=("http://lcamtuf.coredump.cx/p0f3/releases/$pkgname-$pkgver.tgz")
md5sums=('edbc4b135b2646db3227a441268fd2e2')

build() {
	cd "${srcdir}"
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -e 's/\/usr\/local\/include\//\/usr\/include\//g' -i build.sh
	sed -e 's/\/usr\/local\/lib\//\/usr\/lib\//g' -i build.sh
	sed -e 's/"p0f.fp"/"\/etc\/p0f\/p0f.fp"/g' -i config.h
	make

	## additional tools
	if [ $_p0ftools = "y" ]; then
		cd "${srcdir}/${pkgname}-${pkgver}/tools"
		make p0f-client
		make p0f-sendsyn
	fi
	if [ $_p0ftools6 = "y" ]; then
		cd "${srcdir}/${pkgname}-${pkgver}/tools"
		make p0f-sendsyn6
	fi
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -d -Dm755 ${pkgdir}/usr/sbin
	install -d -Dm755 ${pkgdir}/etc/p0f
	install -d -Dm755 ${pkgdir}/usr/share/licenses/${pkgname}
	install -d -Dm755 ${pkgdir}/usr/share/doc/${pkgname}

	install -m744 p0f ${pkgdir}/usr/sbin
	install -m755 p0f.fp ${pkgdir}/etc/p0f
	install -D -m644 docs/COPYING ${pkgdir}/usr/share/licenses/${pkgname}
	install -D -m644 docs/README ${pkgdir}/usr/share/doc/${pkgname}

	## additional tools
	if [ $_p0ftools = "y" ]; then
		install -m744 tools/p0f-client ${pkgdir}/usr/sbin
		install -m744 tools/p0f-sendsyn ${pkgdir}/usr/sbin
	fi
	if [ $_p0ftools6 = "y" ]; then
		install -m744 tools/p0f-sendsyn6 ${pkgdir}/usr/sbin
	fi
}
