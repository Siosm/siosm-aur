# Maintainer: Timoth√©e Ravier <tim@siosm.fr>
# Previous maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libnm-qt-git
pkgver=525.71ebcca
pkgrel=1
pkgdesc='Qt-only wrapper for NetworkManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libnm-qt'
license=('LGPL')
depends=('libmm-qt-git' 'networkmanager')
makedepends=('cmake' 'doxygen' 'git')
provides=('libnm-qt')
conflicts=('libnm-qt')
source=('git://anongit.kde.org/libnm-qt')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname/-git}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname/-git} \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
