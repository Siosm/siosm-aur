# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Based on owncloud-git PKGBUILD by Alexander Ovsyannikov

pkgname=owncloud
pkgver=5.0.13
pkgrel=1
pkgdesc="Cloud server to store your files centrally on a hardware controlled by you"
arch=('any')
url='http://owncloud.org/'
license=('GPL')
depends=('php-gd' 'php-intl')
optdepends=('php-apache: to use the Apache web server'
            'php-sqlite: to use the SQLite database backend'
            'php-pgsql: to use the PostgreSQL database backend'
            'mariadb: to use the MySQL database backend'
            'smbclient: to mount SAMBA shares')
source=("http://download.owncloud.org/community/${pkgname}-${pkgver}.tar.bz2"
        'owncloud.conf')
backup=('etc/httpd/conf/extra/owncloud.conf')
install=owncloud.install
md5sums=('4ce4cf5de3fdf1f70666549c5ecfda7a'
         'c1d49a3c0d8433bed6bffcd21ebb0064')
sha256sums=('8a64ab8cc2ae4c71713a2922e5fbcda029de8ab2cf4dee04e15f041be48aa326'
            'cd63cae0003845c23a95d3347848787107cef1e992705c18c3b125180b32fea4')

package() {
  # Install license
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 ${pkgname}/COPYING-* "${pkgdir}"/usr/share/licenses/${pkgname}

  # Install project
  install -dm 755 "${pkgdir}"/usr/share/webapps/
  cp -a ${pkgname} "${pkgdir}"/usr/share/webapps/

  # Install Apache .conf file
  install -Dm 644 owncloud.conf "${pkgdir}"/etc/httpd/conf/extra/owncloud.conf
}
